apply plugin: 'java'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.6'
    }
}

apply plugin: 'net.ltgt.apt'

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.intellij:annotations:12.0@jar'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.vimeo.stag:stag-library:1.0.1'
    apt 'com.vimeo.stag:stag-library-compiler:1.0.1'
}

group = 'com.vimeo.networking'
version = '1.1.0'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

// Requires apply plugin: maven-publish
publishing {
    publications {
        mavenJava(MavenPublication) {
            // We dont have any complex artifacts, so let's just reference the fact that we
            // rely on plugin java
            from components.java
            groupId project.group
            artifactId 'vimeo-networking'
            version project.version

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// Only execute the bintray task if this is the actual networking project (not an include)
allprojects {
    afterEvaluate { project ->
        def bintrayProject = project.plugins.hasPlugin('com.jfrog.bintray')
        if (bintrayProject) {
            bintray {
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                user = properties.getProperty('bintray.user')
                key = properties.getProperty('bintray.apikey')
                publications = ['mavenJava']
                dryRun = false // Whether to run this as dry-run, without deploying
                pkg {
                    repo = 'maven'
                    name = 'vimeo-networking'
                    userOrg = 'vimeo'
                    licenses = ['MIT']
                    websiteUrl = 'https://github.com/vimeo/vimeo-networking-java'
                    issueTrackerUrl = 'https://github.com/vimeo/vimeo-networking-java/issues'
                    vcsUrl = 'https://github.com/vimeo/vimeo-networking-java.git'
                    labels = ['vimeo', 'android', 'java', 'networking', 'api']
                    publicDownloadNumbers = true
                    version {
                        name = project.version
                        vcsTag = project.version
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
